let
    link "ambutils.fn" as amb;
    import amb operators;
    link "listutils.fn" as list;
    import list operators;

    // Five schoolgirls sat for an examination. Their parents—so they
    // thought—showed an undue degree of interest in the result.
    // They therefore agreed that, in writing home about the examination,
    // each girl should make one true statement and one untrue one.
    // The following are the relevant passages from their letters:
    // 
    // Betty:
    //   “Kitty was second in the examination, I was only third.”
    // Ethel:
    //   “You'll be glad to hear that I was on top. Joan was second.”
    // Joan:
    //   “I was third, and poor old Ethel was bottom.”
    // Kitty:
    //   “I came out second. Mary was only fourth.”
    // Mary:
    //   “I was fourth. Top place was taken by Betty.”
    //
    // What in fact was the order in which the five girls were placed? 
    fn liars() {
        let
            ranks = 1::5;
            betty = from ranks;
            ethel = from ranks except [betty];
            joan  = from ranks except [betty, ethel];
            kitty = from ranks except [betty, ethel, joan];
            mary  = from ranks except [betty, ethel, joan, kitty];
        in
            !: (kitty == 2) xor (betty == 3);
            !: (ethel == 1) xor (joan  == 2);
            !: (joan  == 3) xor (ethel == 5);
            !: (kitty == 2) xor (mary  == 4);
            !: (mary  == 4) xor (betty == 1);

            ["Betty", "Ethel", "Joan", "Kitty", "Mary"]
                zip [betty, ethel, joan, kitty, mary]
                    <> fn (#(_, a), #(_, b)) { a <=> b }
                        |> fn (#(x, _)) { x };
    }
in
    print liars()
