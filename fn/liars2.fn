let
    import "ambutils.fn" as amb;
    import "listutils.fn" as list;

    // Five schoolgirls sat for an examination. Their parents—so they
    // thought—showed an undue degree of interest in the result.
    // They therefore agreed that, in writing home about the examination,
    // each girl should make one true statement and one untrue one.
    // The following are the relevant passages from their letters:
    // 
    // Betty:
    //   “Kitty was second in the examination, I was only third.”
    // Ethel:
    //   “You'll be glad to hear that I was on top. Joan was second.”
    // Joan:
    //   “I was third, and poor old Ethel was bottom.”
    // Kitty:
    //   “I came out second. Mary was only fourth.”
    // Mary:
    //   “I was fourth. Top place was taken by Betty.”
    //
    // What in fact was the order in which the five girls were placed? 
    fn liars() {
        let
            ranks = [1, 2, 3, 4, 5];
            betty = amb.one_of(ranks);
            ethel = amb.one_of(list.exclude([betty], ranks));
            joan = amb.one_of(list.exclude([betty, ethel], ranks));
            kitty = amb.one_of(list.exclude([betty, ethel, joan], ranks));
            mary = amb.one_of(list.exclude([betty, ethel, joan, kitty], ranks));
        in
            amb.require((kitty == 2) xor (betty == 3));
            amb.require((ethel == 1) xor (joan == 2));
            amb.require((joan == 3) xor (ethel == 5));
            amb.require((kitty == 2) xor (mary == 4));
            amb.require((mary == 4) xor (betty == 1));
            list.map(
                fn (#(x, _)) { x },
                list.sortBy(
                    fn (#(_, a), #(_, b)) { a <=> b },
                    [
                        #("Betty", betty),
                        #("Ethel", ethel),
                        #("Joan", joan),
                        #("Kitty", kitty),
                        #("Mary", mary)
                    ]
                )
            )
    }
in
    print(liars())
