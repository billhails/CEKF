let
    link "ambutils.fn" as amb;
    import amb operators;
    link "listutils.fn" as lst;
    import lst operators;

    // A wine merchant has six barrels of wine and beer containing:
    //
    //    30 gallons
    //    32 gallons
    //    36 gallons
    //    38 gallons
    //    40 gallons
    //    62 gallons
    //
    // Five barrels are filled with wine and one with beer.
    // The first customer purchases two barrels of wine.
    // The second customer purchases twice as much wine as
    // the first customer.
    //
    // Which barrel contains beer? 
    fn barrels_of_fun() {
        let
            barrels = [30, 32, 36, 38, 40, 62];
            beer = one barrels;
            wine = barrels except [beer];
            wine_1 = one wine;
            wine_2 = one wine except [wine_1];
            purchase = some wine except [wine_1, wine_2];
        in
            !: (wine_1 + wine_2) * 2 == total purchase;
            beer
    }
in
    print(barrels_of_fun())
