namespace

link "minexpr.fn" as M;
link "../listutils.fn" as list;
import list operators;

// (M.expr -> M.expr) -> M.expr -> M.expr
fn bottom_up(f, exp) {
    f(_transform(bottom_up(f), exp))
}

// (M.expr -> M.expr) -> M.expr -> M.expr
fn top_down(f, exp) {
    _transform(top_down(f), f(exp))
}

// (M.expr -> M.expr) -> M.expr -> M.expr
fn _transform(t, exp) {
    switch (exp) {

        (M.amb_expr(e1, e2)) {
            M.amb_expr(t(e1), t(e2))
        }

        (M.back_expr) |
        (M.primop(_)) |
        (M.character(_)) |
        (M.env_expr) |
        (M.error_expr) |
        (M.stdint(_)) |
        (M.var(_)) |
        (M.bigint(_)) { exp }


        (M.apply_closure(fun, args)) {
            M.apply_closure(t(fun), args |> t)
        }

        (M.apply(fun, args)) {
            M.apply(t(fun), args |> t)
        }

        (M.callcc_expr(e)) {
            M.callcc_expr(t(e))
        }

        (M.cond_expr(test, branches)) {
            M.cond_expr(t(test), branches |> t && t)
        }

        (M.env_ref(e, s)) {
            M.env_ref(t(e), s)
        }

        (M.if_expr(test, consequent, alternative)) {
            M.if_expr(t(test), t(consequent), t(alternative))
        }

        (M.lambda(params, body)) {
            M.lambda(params, t(body))
        }

        (M.lambdac(params, body)) {
            M.lambdac(params, t(body))
        }

        (M.letrec_expr(bindings, body)) {
            M.letrec_expr(bindings |> identity && (t && identity), t(body))
        }

        (M.lookup(s, n, e)) {
            M.lookup(s, n, t(e))
        }

        (M.make_closure(lam, env)) {
            M.make_closure(t(lam), t(env))
        }

        (M.make_env(bindings)) {
            M.make_env(bindings |> identity && t)
        }

        (M.make_vec(size, elements)) {
            M.make_vec(size, elements |> t)
        }

        (M.match_cases(e, cases)) {
            M.match_cases(t(e), cases |> identity && t)
        }

        (M.namespaces(exprs)) {
            M.namespaces(exprs |> t)
        }

        (M.sequence(exps)) {
            M.sequence(exps |> t)
        }

        (_) {
            M.print_expr(exp);
            puts(": ");
            error("Unhandled expression type in transform")
        }
    };
}