namespace
link "../listutils.fn" as list;
link "../dictutils.fn" as DICT;
link "expr.fn" as E;
link "transform.fn" as TR;
link "freevars.fn" as FV;
link "gensym.fn" as GS;
link "subst.fn" as SUBST;

fn closure_convert {
    (exp=E.lambda(params, body)) {
        let
            senv = GS.genstring("env");
            sparams = senv @ params;
            fv = FV.free(exp);
            venv = list.map(fn (v) { #(v, E.var(v)) }, fv);
            sub = DICT.make(fv, list.map(fn (v) { E.env_ref(E.var(senv), v) }, fv));
            vbody = SUBST.substitute(sub, body);
        in
            E.make_closure(E.lambdac(sparams, vbody), E.make_env(venv))
    }
    (E.apply(func, args)) {
        E.apply_closure(func, args)
    }
    (x) { x }
}

fn flat_closure_convert(exp) { TR.bottom_up(closure_convert, exp) }
fn shared_closure_convert(exp) { TR.top_down(closure_convert, exp) }