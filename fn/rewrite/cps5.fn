let
    link "expr.fn" as E;
    link "../listutils.fn" as list;
    link "../ioutils.fn" as io;
    import io operator "$_";

    fn genstring(p) { p @@ $incr() }
    fn gensym(p) { E.var(genstring(p)) }

    fn isAexpr {
        (E.var(_)) |
        (E.back_expr) |
        (E.character(_)) |
        (E.constant(_)) |
        (E.constructor_info(_)) |
        (E.env_expr) |
        (E.error_expr) |
        (E.lambda(_, _)) |
        (E.stdint(_)) |
        (E.bigint(_)) { true }
        (_) { false }
    }

    fn M {
        (E.lambda(vars, body)) {
            let c = genstring("$k");
            in E.lambda(vars @@ [c], T_c(body, E.var(c)))
        }
        (x) { x }
    }

    fn kToC(k) {
        let rv = genstring("$rv");
        in E.lambda([rv], k(E.var(rv)))
    }

    fn T_k(e, k) {
        if (isAexpr(e)) {
            k(M(e))
        } else {
            switch (e)  {
                (E.amb_expr(expr1, expr2)) {
                    let
                        c = kToC(k);
                    in
                        E.amb_expr(T_c(expr1, c), T_c(expr2, c))
                }

                (E.apply(_, _)) {
                    let
                        c = kToC(k);
                    in
                        T_c(e, c)
                }

                (E.callcc_expr(e)) {
                    let
                        c = kToC(k);
                    in
                        T_c(E.callcc_expr(e), c)
                }

                (E.cond_expr(test, branches)) {
                    let
                        c = kToC(k);
                    in
                        T_k(test, fn (atest) {
                            E.cond_expr(atest, list.map(fn {(#(val, result)) {
                                #(val, T_c(result, c))
                            }}, branches))
                        })
                }

                (E.construct(name, args)) {
                    Ts_k(args, fn (sargs) {
                        k(E.construct(name, sargs))
                    })
                }

                (E.deconstruct(name, index, expr)) {
                    T_k(expr, fn (sexpr) {
                        k(E.deconstruct(name, index, sexpr))
                    })
                }

                (E.if_expr(exprc, exprt, exprf)) {
                    let
                        c = kToC(k);
                    in
                        T_k(exprc, fn(aexp) {
                            E.if_expr(aexp, T_c(exprt, c), T_c(exprf, c))
                        })
                }

                (E.letrec_expr(bindings, expr)) {
                    let
                        #(vars, aexps) = list.unzip(bindings);
                    in
                        E.letrec_expr(list.zip(vars, list.map(M, aexps)), T_k(expr, k))
                }

                (E.let_expr(bindings, expr)) {
                    let
                        #(vars, exps) = list.unzip(bindings);
                    in
                        T_k(E.apply(E.lambda(vars, expr), exps), k)
                }

                (E.letstar_expr(bindings, expr)) {
                    let
                        fn nest_lets {
                            ([], body) { body }
                            (#(var, exp) @ rest, body) {
                                E.let_expr([#(var, exp)], nest_lets(rest, body))
                            }
                        }
                    in
                        T_k(nest_lets(bindings, expr), k)
                }

                (E.lookup(name, index, expr)) {
                    E.lookup(name, index, T_k(expr, k))
                }

                (E.make_tuple(args)) {
                    Ts_k(args, fn (sargs) {
                        k(E.make_tuple(sargs))
                    })
                }

                (E.make_vec(size, args)) {
                    Ts_k(args, fn (sargs) {
                        k(E.make_vec(size, sargs))
                    })
                }

                (E.match_cases(test, cases)) {
                    let
                        c = kToC(k);
                    in
                        T_k(test, fn (atest) {
                            E.match_cases(atest, list.map(fn {(#(indices, result)) {
                                #(indices, T_c(result, c))
                            }}, cases))
                        })
                }

                (E.namespaces(exprs)) {
                    Ts_k(exprs, fn (sexprs) {
                        k(E.namespaces(sexprs))
                    })
                }

                (E.primapp(p, e1, e2)) {
                    T_k(e1, fn (s1) {
                        T_k(e2, fn (s2) {
                            k(E.primapp(p, s1, s2))
                        })
                    })
                }

                (E.print_exp(expr)) {
                    T_k(expr, fn (sexpr) {
                        k(E.print_exp(sexpr))
                    })
                }

                (E.sequence([expr])) { T_k(expr, k) }

                (E.sequence(expr @ exprs)) {
                    T_k(expr, fn (ignored) {
                        T_k(E.sequence(exprs), k)
                    })
                }

                (E.tag(expr)) {
                    T_k(expr, fn (sexpr) {
                        k(E.tag(sexpr))
                    })
                }

                (E.tuple_index(size, index, expr)) {
                    T_k(expr, fn (sexpr) {
                        k(E.tuple_index(size, index, sexpr))
                    })
                }

                (E.typedefs(defs, expr)) {
                    E.typedefs(defs, T_k(expr, k))
                }

                (E.typeof_expr(expr)) {
                    T_k(expr, fn (sexpr) {
                        k(E.typeof_expr(sexpr))
                    })
                }

                (x) {
                    E.print_expr(x);
                    puts("\n");
                    error("T_k: unsupported expression")
                }
            }
        }
    }

    fn T_c(expr, c) {
        if (isAexpr(expr)) {
            E.apply(c, [M(expr)])
        } else {
            switch(expr) {
                (E.amb_expr(expr1, expr2)) {
                    let
                        sk = genstring("$k");
                        vsk = E.var(sk);
                    in
                        E.apply(E.lambda([sk], E.amb_expr(T_c(expr1, vsk), T_c(expr2, vsk))), [c])
                }

                (E.apply(f, es)) {
                    T_k(f, fn(sf) {
                        Ts_k(es, fn (ses) {E.apply(sf, ses @@ [c])})
                    })
                }

                (E.callcc_expr(e)) {
                    T_k(e, fn (sf) {
                        E.apply(
                            E.parse("(lambda (f cc) (f (lambda (x i) (cc x)) cc))"),
                            [sf, c]
                        )
                    })
                }

                (E.cond_expr(test, branches)) {
                    let
                        sk = genstring("$k");
                        vsk = E.var(sk);
                    in
                        E.apply(E.lambda([sk], T_k(test, fn (atest) {
                            E.cond_expr(atest, list.map(fn {(#(val, result)) {
                                #(val, T_c(result, vsk))
                            }}, branches))
                        })), [c])
                }

                (E.construct(name, args)) {
                    Ts_k(args, fn (sargs) {
                        E.apply(c, [E.construct(name, sargs)])
                    })
                }

                (E.deconstruct(name, index, expr)) {
                    T_k(expr, fn (sexpr) {
                        E.apply(c, [E.deconstruct(name, index, sexpr)])
                    })
                }

                (E.if_expr(exprc, exprt, exprf)) {
                    let
                        sk = genstring("$k");
                        vsk = E.var(sk);
                    in
                        E.apply(E.lambda([sk], T_k(exprc, fn (aexp) {
                            E.if_expr(aexp, T_c(exprt,vsk), T_c(exprf,vsk))
                        })), [c])
                }

                (E.let_expr(bindings, expr)) {
                    let
                        #(vars, exps) = list.unzip(bindings);
                    in
                        T_c(E.apply(E.lambda(vars, expr), exps), c)
                }

                (E.letstar_expr(bindings, expr)) {
                    let
                        fn nest_lets {
                            ([], body) { body }
                            (#(var, exp) @ rest, body) {
                                E.let_expr([#(var, exp)], nest_lets(rest, body))
                            }
                        }
                    in
                        T_c(nest_lets(bindings, expr), c)
                }

                (E.letrec_expr(bindings, expr)) {
                    let
                        #(vars, aexps) = list.unzip(bindings);
                    in
                        E.letrec_expr(list.zip(vars, list.map(M, aexps)), T_c(expr, c))
                }

                (E.lookup(name, index, expr)) {
                    E.lookup(name, index, T_c(expr, c))
                }

                (E.make_tuple(args)) {
                    Ts_k(args, fn (sargs) {
                        E.apply(c, [E.make_tuple(sargs)])
                    })
                }

                (E.make_vec(size, args)) {
                    Ts_k(args, fn (sargs) {
                        E.apply(c, [E.make_vec(size, sargs)])
                    })
                }

                (E.match_cases(test, cases)) {
                    let
                        sk = genstring("$k");
                        vsk = E.var(sk);
                    in
                        E.apply(E.lambda([sk], T_k(test, fn (atest) {
                            E.match_cases(atest, list.map(fn {(#(indices, result)) {
                                #(indices, T_c(result, vsk))
                            }}, cases))
                        })), [c])
                }

                (E.namespaces(exprs)) {
                    Ts_k(exprs, fn (sexprs) {
                        E.apply(c, [E.namespaces(sexprs)])
                    })
                }

                (E.primapp(p, e1, e2)) {
                    T_k(e1, fn (s1) {
                        T_k(e2, fn (s2) {
                            E.apply(c, [E.primapp(p, s1, s2)])
                        })
                    })
                }

                (E.print_exp(expr)) {
                    T_k(expr, fn (sexpr) {
                        E.apply(c, [E.print_exp(sexpr)])
                    })
                }

                (E.sequence([expr])) { T_c(expr, c) }

                (E.sequence(expr @ exprs)) {
                    T_k(expr, fn (ignored) {
                        T_c(E.sequence(exprs), c)
                    })
                }

                (E.tag(expr)) {
                    T_k(expr, fn (sexpr) {
                        E.apply(c, [E.tag(sexpr)])
                    })
                }

                (E.tuple_index(size, index, expr)) {
                    T_k(expr, fn (sexpr) {
                        E.apply(c, [E.tuple_index(size, index, sexpr)])
                    })
                }

                (E.typedefs(defs, expr)) {
                    E.typedefs(defs, T_c(expr, c))
                }

                (E.typeof_expr(expr)) {
                    T_k(expr, fn (sexpr) {
                        E.apply(c, [E.typeof_expr(sexpr)])
                    })
                }

                (x) {
                    E.print_expr(x);
                    puts("\n");
                    error("T_c: unsupported expression")
                }
            }
        }
    }

    fn Ts_k {
        ([], k) { k([]) }
        (h @ t, k) {
            T_k(h, fn (hd) {
                Ts_k(t, fn (tl) {
                    k(hd @ tl)
                })
            })
        }
    }

in
    list.for_each(fn (str) {
        let expr = E.parse(str);
        in
            E.print_expr(expr);
            puts(" ==>\n    ");
            E.print_expr(T_c(expr, E.var("halt")));
            puts("\n\n")
    },
    [
        // Basic application
        "(g a)",

        // Simple lambda application
        "((lambda (x) (h x)) (g 4))",

        // Lambda definition with primitives
        "(lambda (a b) (+ a (* b 2)))",

        // Lambda application with primitives - nested
        "((lambda (a b) (+ a (* b 2))) 3 4)",

        // Lambda application with function call in primitive
        "((lambda (a b) (+ a (* (f b) 2))) 3 4)",

        // Amb operator (non-determinism)
        "(amb 1 2)",

        // Simple call/cc - immediate escape
        "(call/cc (lambda (k) (k 5)))",

        // call/cc that doesn't escape
        "(call/cc (lambda (k) 42))",

        // call/cc with computation before escape
        "(call/cc (lambda (k) (+ 10 (k 5))))",

        // Nested call/cc
        "(call/cc (lambda (k1) (call/cc (lambda (k2) (k1 (k2 7))))))",

        // call/cc with amb
        "(call/cc (lambda (k) (amb (k 1) (k 2))))",

        // Factorial with letrec
        "(letrec ((fact (lambda (n) (if (= n 0) 1 (* n (fact (- n 1))))))) (fact 5))",

        // Multiple argument function
        "((lambda (x y z) (+ x (+ y z))) 1 2 3)",

        // Nested lambdas (currying)
        "((lambda (x) (lambda (y) (+ x y))) 5)",

        // Application of curried function
        "(((lambda (x) (lambda (y) (+ x y))) 5) 3)",

        // If expression with complex branches
        "(if (= x 0) (f 1) (g 2))",

        // If with nested if
        "(if (= x 0) (if (= y 0) 1 2) 3)",

        // Sequence (begin in Racket)
        "(sequence (f 1) (g 2) (h 3))",

        // Letrec with mutual recursion
        "(letrec ((even (lambda (n) (if (= n 0) true (odd (- n 1))))) (odd (lambda (n) (if (= n 0) false (even (- n 1)))))) (even 5))",

        // Complex amb with computation
        "(+ (amb 1 2) (amb 3 4))",

        // Nested application
        "(f (g (h x)))",

        // Multiple primitives in sequence
        "(+ (* 2 3) (- 5 1))",

        // call/cc capturing continuation in letrec
        "(letrec ((k null)) (call/cc (lambda (cont) (sequence (k = cont) 42))))",

        // Lambda with no arguments
        "((lambda () 42))",

        // call/cc with primitive operations
        "(call/cc (lambda (k) (if (= 1 1) (k 10) 20)))",

        // Deeply nested primitives
        "(+ 1 (+ 2 (+ 3 (+ 4 5))))",

        // Application with multiple complex arguments
        "(f (g a) (h b) (i c))",

        // If where branches have applications
        "(if test (f (g x)) (h (i y)))",

        // Sequence with amb
        "(sequence (amb 1 2) (amb 3 4))",

        // call/cc that passes continuation to another function
        "(call/cc (lambda (k) (f k)))",

        // cond expression - like switch/case
        "(cond x (1 (result1)) (2 (result2)) (3 (result3)))",

        // cond with complex test and results
        "(cond (f x) (1 (g 1)) (2 (h 2)))",

        // construct with atomic arguments
        "(construct pair 1 2)",

        // construct with complex arguments
        "(construct pair (f 1) (g 2))",

        // nested construct
        "(construct cons (f x) (construct cons (g y) (constant nil)))",

        // deconstruct with atomic argument
        "(deconstruct pair 0 p)",

        // deconstruct with complex argument
        "(deconstruct pair 1 (f x))",

        // nested deconstruct
        "(deconstruct pair 0 (deconstruct pair 1 (f x)))",

        // let with atomic value
        "(let ((x 42)) (f x))",

        // let with complex value
        "(let ((x (f 10))) (g x))",

        // nested let
        "(let ((x (f 1)) (y (g x))) (h x y))",

        // nested let*
        "(let* ((x (f 1)) (y (g x))) (h x y))",

        // lookup with simple variable
        "(lookup ns 0 x)",

        // lookup with function call
        "(lookup ns 0 (f x))",

        // nested lookup
        "(f (lookup ns 0 (g x)))",

        // make_tuple with atomic args
        "(make_tuple 1 2 3)",

        // make_tuple with complex args
        "(make_tuple (f 1) (g 2))",

        // make_tuple with mixed args
        "(make_tuple x (f y) 42)",

        // make_vec with atomic args
        "(make_vec 3 1 2 3)",

        // make_vec with complex args
        "(make_vec 2 (f 1) (g 2))",

        // make_vec with mixed args
        "(make_vec 3 x (f y) 42)",

        // match_cases with atomic test
        "(match_cases x ((1 2) (result1)) ((3 4 5) (result2)))",

        // match_cases with complex test
        "(match_cases (f x) ((1) (g 1)) ((2) (h 2)))",

        // match_cases with complex results
        "(match_cases x ((1) (f 1)) ((2) (g 2)))",

        // namespaces with atomic expressions
        "(namespaces x y z)",

        // namespaces with complex expressions
        "(namespaces (f 1) (g 2))",

        // namespaces with mixed expressions
        "(namespaces x (f y) z)",

        // namespaces with letrec bodies returning env
        "(namespaces (letrec ((x 1) (y 2)) (env)) (letrec ((a 3) (b 4)) (env)))",

        // print_exp with atomic argument
        "(print x)",

        // print_exp with complex argument
        "(print (f x))",

        // print_exp nested in computation
        "(+ (print (f x)) 10)",

        // tag with atomic argument
        "(tag x)",

        // tag with complex argument
        "(tag (f x))",

        // tag in cond test
        "(cond (tag (f x)) (1 (result1)) (2 (result2)))",

        // tuple_index with atomic tuple
        "(tuple_index 3 0 t)",

        // tuple_index with complex tuple
        "(tuple_index 2 1 (f x))",

        // nested tuple_index
        "(tuple_index 2 0 (tuple_index 3 1 (f x)))",

        // typedefs with simple expression
        "(typedefs 0 x)",

        // typedefs with letrec body
        "(typedefs 0 (letrec ((f (lambda (x) (g x)))) (f 42)))",

        // typedefs with complex body
        "(typedefs 0 (f (g x)))",

        // typedefs wrapping letrec with env body
        "(typedefs 0 (letrec ((x 1) (y 2)) (env)))",

        // typeof_expr with atomic argument
        "(typeof x)",

        // typeof_expr with complex argument
        "(typeof (f x))",

        // typeof_expr in conditional
        "(if (typeof (f x)) (g 1) (h 2))"
    ]);

// output:
//
// (g a) ==>
//     (g a halt)
//
// ((λ (x) (h x)) (g 4)) ==>
//     (g 4 (λ ($rv2) ((λ (x $k1) (h x $k1)) $rv2 halt)))
//
// (λ (a b) (+ a (* b 2))) ==>
//     (halt (λ (a b $k3) ($k3 (+ a (* b 2)))))
//
// ((λ (a b) (+ a (* b 2))) 3 4) ==>
//     ((λ (a b $k4) ($k4 (+ a (* b 2)))) 3 4 halt)
//
// ((λ (a b) (+ a (* (f b) 2))) 3 4) ==>
//     ((λ (a b $k5) (f b (λ ($rv6) ($k5 (+ a (* $rv6 2)))))) 3 4 halt)
//
// (amb 1 2) ==>
//     ((λ ($k7) (amb ($k7 1) ($k7 2))) halt)
//
// (call/cc (λ (k) (k 5))) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k $k8) (k 5 $k8)) halt)
//
// (call/cc (λ (k) 42)) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k $k9) ($k9 42)) halt)
//
// (call/cc (λ (k) (+ 10 (k 5)))) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k $k10) (k 5 (λ ($rv11) ($k10 (+ 10 $rv11))))) halt)
//
// (call/cc (λ (k1) (call/cc (λ (k2) (k1 (k2 7)))))) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k1 $k12) ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k2 $k13) (k2 7 (λ ($rv14) (k1 $rv14 $k13)))) $k12)) halt)
//
// (call/cc (λ (k) (amb (k 1) (k 2)))) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k $k15) ((λ ($k16) (amb (k 1 $k16) (k 2 $k16))) $k15)) halt)
//
// (letrec ((fact (λ (n) (if (= n 0) 1 (* n (fact (- n 1))))))) (fact 5)) ==>
//     (letrec ((fact (λ (n $k17) ((λ ($k18) (= n 0 (λ ($rv19) (if $rv19 ($k18 1) (fact (- n 1) (λ ($rv20) ($k18 (* n $rv20)))))))) $k17)))) (fact 5 halt))
//
// ((λ (x y z) (+ x (+ y z))) 1 2 3) ==>
//     ((λ (x y z $k21) ($k21 (+ x (+ y z)))) 1 2 3 halt)
//
// ((λ (x) (λ (y) (+ x y))) 5) ==>
//     ((λ (x $k22) ($k22 (λ (y $k23) ($k23 (+ x y))))) 5 halt)
//
// (((λ (x) (λ (y) (+ x y))) 5) 3) ==>
//     ((λ (x $k25) ($k25 (λ (y $k26) ($k26 (+ x y))))) 5 (λ ($rv24) ($rv24 3 halt)))
//
// (if (= x 0) (f 1) (g 2)) ==>
//     ((λ ($k27) (= x 0 (λ ($rv28) (if $rv28 (f 1 $k27) (g 2 $k27))))) halt)
//
// (if (= x 0) (if (= y 0) 1 2) 3) ==>
//     ((λ ($k29) (= x 0 (λ ($rv30) (if $rv30 ((λ ($k31) (= y 0 (λ ($rv32) (if $rv32 ($k31 1) ($k31 2))))) $k29) ($k29 3))))) halt)
//
// (sequence (f 1) (g 2) (h 3)) ==>
//     (f 1 (λ ($rv33) (g 2 (λ ($rv34) (h 3 (λ ($rv35) (sequence $rv33 $rv34 $rv35 halt)))))))
//
// (letrec ((even (λ (n) (if (= n 0) true (odd (- n 1)))))(odd (λ (n) (if (= n 0) false (even (- n 1)))))) (even 5)) ==>
//     (letrec ((even (λ (n $k36) ((λ ($k37) (= n 0 (λ ($rv38) (if $rv38 ($k37 true) (odd (- n 1) $k37))))) $k36)))(odd (λ (n $k39) ((λ ($k40) (= n 0 (λ ($rv41) (if $rv41 ($k40 false) (even (- n 1) $k40))))) $k39)))) (even 5 halt))
//
// (+ (amb 1 2) (amb 3 4)) ==>
//     (amb ((λ ($rv42) (amb ((λ ($rv43) (halt (+ $rv42 $rv43))) 3) ((λ ($rv43) (halt (+ $rv42 $rv43))) 4))) 1) ((λ ($rv42) (amb ((λ ($rv43) (halt (+ $rv42 $rv43))) 3) ((λ ($rv43) (halt (+ $rv42 $rv43))) 4))) 2))
//
// (f (g (h x))) ==>
//     (h x (λ ($rv45) (g $rv45 (λ ($rv44) (f $rv44 halt)))))
//
// (+ (* 2 3) (- 5 1)) ==>
//     (halt (+ (* 2 3) (- 5 1)))
//
// (letrec ((k null)) (call/cc (λ (cont) (sequence (k = cont) 42)))) ==>
//     (letrec ((k null)) ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (cont $k46) (k = cont (λ ($rv47) (sequence $rv47 42 $k46)))) halt))
//
// ((λ () 42)) ==>
//     ((λ ($k48) ($k48 42)) halt)
//
// (call/cc (λ (k) (if (= 1 1) (k 10) 20))) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k $k49) ((λ ($k50) (= 1 1 (λ ($rv51) (if $rv51 (k 10 $k50) ($k50 20))))) $k49)) halt)
//
// (+ 1 (+ 2 (+ 3 (+ 4 5)))) ==>
//     (halt (+ 1 (+ 2 (+ 3 (+ 4 5)))))
//
// (f (g a) (h b) (i c)) ==>
//     (g a (λ ($rv52) (h b (λ ($rv53) (i c (λ ($rv54) (f $rv52 $rv53 $rv54 halt)))))))
//
// (if test (f (g x)) (h (i y))) ==>
//     ((λ ($k55) (if test (g x (λ ($rv56) (f $rv56 $k55))) (i y (λ ($rv57) (h $rv57 $k55))))) halt)
//
// (sequence (amb 1 2) (amb 3 4)) ==>
//     (amb ((λ ($rv58) (amb ((λ ($rv59) (sequence $rv58 $rv59 halt)) 3) ((λ ($rv59) (sequence $rv58 $rv59 halt)) 4))) 1) ((λ ($rv58) (amb ((λ ($rv59) (sequence $rv58 $rv59 halt)) 3) ((λ ($rv59) (sequence $rv58 $rv59 halt)) 4))) 2))
//
// (call/cc (λ (k) (f k))) ==>
//     ((λ (f cc) (f (λ (x i) (cc x)) cc)) (λ (k $k60) (f k $k60)) halt)
//
// (cond x (1 (result1)) (2 (result2)) (3 (result3))) ==>
//     ((λ ($k61) (cond x (1 (result1 $k61)) (2 (result2 $k61)) (3 (result3 $k61)))) halt)
//
// (cond (f x) (1 (g 1)) (2 (h 2))) ==>
//     ((λ ($k62) (f x (λ ($rv63) (cond $rv63 (1 (g 1 $k62)) (2 (h 2 $k62)))))) halt)
//
// (construct pair 1 2) ==>
//     (halt (construct pair 1 2))
//
// (construct pair (f 1) (g 2)) ==>
//     (f 1 (λ ($rv64) (g 2 (λ ($rv65) (halt (construct pair $rv64 $rv65))))))
//
// (construct cons (f x) (construct cons (g y) (constant nil))) ==>
//     (f x (λ ($rv66) (g y (λ ($rv67) (halt (construct cons $rv66 (construct cons $rv67 (constant nil))))))))
//
// (deconstruct pair 0 p) ==>
//     (halt (deconstruct pair 0 p))
//
// (deconstruct pair 1 (f x)) ==>
//     (f x (λ ($rv68) (halt (deconstruct pair 1 $rv68))))
//
// (deconstruct pair 0 (deconstruct pair 1 (f x))) ==>
//     (f x (λ ($rv69) (halt (deconstruct pair 0 (deconstruct pair 1 $rv69)))))
//
// (let (x 42) (f x)) ==>
//     (let (x 42) (f x halt))
//
// (let (x (f 10)) (g x)) ==>
//     (f 10 (λ ($rv70) (let (x $rv70) (g x halt))))
//
// (let (x (f 1)) (let (y (g x)) (h x y))) ==>
//     (f 1 (λ ($rv71) (let (x $rv71) (g x (λ ($rv72) (let (y $rv72) (h x y halt)))))))
//
// (lookup ns 0 x) ==>
//     (lookup ns 0 (halt x))
//
// (lookup ns 0 (f x)) ==>
//     (lookup ns 0 (f x halt))
//
// (f (lookup ns 0 (g x))) ==>
//     (lookup ns 0 (g x (λ ($rv73) (f $rv73 halt))))
//
// (make_tuple 1 2 3) ==>
//     (halt (make_tuple 1 2 3))
//
// (make_tuple (f 1) (g 2)) ==>
//     (f 1 (λ ($rv74) (g 2 (λ ($rv75) (halt (make_tuple $rv74 $rv75))))))
//
// (make_tuple x (f y) 42) ==>
//     (f y (λ ($rv76) (halt (make_tuple x $rv76 42))))
//
// (make_vec 3 1 2 3) ==>
//     (halt (make_vec 3 1 2 3))
//
// (make_vec 2 (f 1) (g 2)) ==>
//     (f 1 (λ ($rv77) (g 2 (λ ($rv78) (halt (make_vec 2 $rv77 $rv78))))))
//
// (make_vec 3 x (f y) 42) ==>
//     (f y (λ ($rv79) (halt (make_vec 3 x $rv79 42))))
//
// (match_cases x ((1 2) (result1)) ((3 4 5) (result2))) ==>
//     ((λ ($k80) (match_cases x ((1 2) (result1 $k80)) ((3 4 5) (result2 $k80)))) halt)
//
// (match_cases (f x) ((1) (g 1)) ((2) (h 2))) ==>
//     ((λ ($k81) (f x (λ ($rv82) (match_cases $rv82 ((1) (g 1 $k81)) ((2) (h 2 $k81)))))) halt)
//
// (match_cases x ((1) (f 1)) ((2) (g 2))) ==>
//     ((λ ($k83) (match_cases x ((1) (f 1 $k83)) ((2) (g 2 $k83)))) halt)
//
// (namespaces x y z) ==>
//     (halt (namespaces x y z))
//
// (namespaces (f 1) (g 2)) ==>
//     (f 1 (λ ($rv84) (g 2 (λ ($rv85) (halt (namespaces $rv84 $rv85))))))
//
// (namespaces x (f y) z) ==>
//     (f y (λ ($rv86) (halt (namespaces x $rv86 z))))
//
// (namespaces (letrec ((x 1)(y 2)) (env)) (letrec ((a 3)(b 4)) (env))) ==>
//     (letrec ((x 1)(y 2)) (letrec ((a 3)(b 4)) (halt (namespaces (env) (env)))))
//
// (print x) ==>
//     (halt (print x))
//
// (print (f x)) ==>
//     (f x (λ ($rv87) (halt (print $rv87))))
//
// (+ (print (f x)) 10) ==>
//     (f x (λ ($rv88) (halt (+ (print $rv88) 10))))
//
// (tag x) ==>
//     (halt (tag x))
//
// (tag (f x)) ==>
//     (f x (λ ($rv89) (halt (tag $rv89))))
//
// (cond (tag (f x)) (1 (result1)) (2 (result2))) ==>
//     ((λ ($k90) (f x (λ ($rv91) (cond (tag $rv91) (1 (result1 $k90)) (2 (result2 $k90)))))) halt)
//
// (tuple_index 1 1 t) ==>
//     (halt (tuple_index 1 1 t))
//
// (tuple_index 1 1 (f x)) ==>
//     (f x (λ ($rv92) (halt (tuple_index 1 1 $rv92))))
//
// (tuple_index 1 1 (tuple_index 1 1 (f x))) ==>
//     (f x (λ ($rv93) (halt (tuple_index 1 1 (tuple_index 1 1 $rv93)))))
//
// (typedefs 1 x) ==>
//     (typedefs 1 (halt x))
//
// (typedefs 1 (letrec ((f (λ (x) (g x)))) (f 42))) ==>
//     (typedefs 1 (letrec ((f (λ (x $k94) (g x $k94)))) (f 42 halt)))
//
// (typedefs 1 (f (g x))) ==>
//     (typedefs 1 (g x (λ ($rv95) (f $rv95 halt))))
//
// (typedefs 1 (letrec ((x 1)(y 2)) (env))) ==>
//     (typedefs 1 (letrec ((x 1)(y 2)) (halt (env))))
//
// (typeof x) ==>
//     (halt (typeof x))
//
// (typeof (f x)) ==>
//     (f x (λ ($rv96) (halt (typeof $rv96))))
//
// (if (typeof (f x)) (g 1) (h 2)) ==>
//     ((λ ($k97) (f x (λ ($rv98) (if (typeof $rv98) (g 1 $k97) (h 2 $k97))))) halt)
//