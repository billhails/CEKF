namespace

// β-reduction

link "minexpr.fn" as M;
link "env.fn" as Env;
link "subst.fn" as S;
link "../listutils.fn" as list;
link "../dictutils.fn" as D;
import list operator "_|>_";
import list operator "_&&_";
import list operator "_take_";
import list operator "_drop_";
import list operator "length_";

fn reduce {
    (M.amb_expr(expr1, expr2)) {
        M.amb_expr(reduce(expr1), reduce(expr2))
    }

    (M.apply(fun, aargs)) {
        let redaargs = aargs |> reduce;
        in
        switch (fun) {
            (M.lambda(fargs, body)) {
                // handle currying
                switch (length fargs <=> length redaargs) {
                    (lt) {
                        // ((λ (f1) body) a1 a2) => reduce(body[f1/a1] a2)
                        let newaargs = redaargs take length fargs;
                            restaargs = redaargs drop length fargs;
                            newbody = S.substitute(D.make(fargs, newaargs), body);
                        in  reduce(M.apply(reduce(newbody), restaargs))
                    }
                    (gt) {
                        // ((λ (f1 f2) body) a1) => (λ (f2) body[f1/a1])
                        let newfargs = fargs take length redaargs;
                            newbody = S.substitute(D.make(newfargs, redaargs), body);
                            restfargs = fargs drop length redaargs;
                        in  M.lambda(restfargs, newbody);
                    }
                    (eq) {
                        // ((λ (f1 f2) body) a1 a2) => reduce(body[f1/a1, f2/a2])
                        let newbody = S.substitute(D.make(fargs, redaargs), body);
                        in  reduce(newbody)
                    }
                }
            }

            (x) {
                M.apply(reduce(x), redaargs)
            }
        }
    }

    (x = M.back_expr) |
    (x = M.primop(_)) |
    (x = M.env_expr) |
    (x = M.error_expr) |
    (x = M.bigint(_)) |
    (x = M.character(_)) |
    (x = M.var(_)) |
    (x = M.stdint(_)) {
        x
    }

    (M.callcc_expr(e)) {
        M.callcc_expr(reduce(e))
    }

    (M.cond_expr(test, branches)) {
        M.cond_expr(reduce(test), branches |> reduce && reduce)
    }

    (M.if_expr(exprc, exprt, exprf)) {
        M.if_expr(reduce(exprc), reduce(exprt), reduce(exprf))
    }

    (M.lambda(params, body)) {
        M.lambda(params, reduce(body))
    }

    (M.letrec_expr(bindings, expr)) {
        M.letrec_expr(bindings |> identity && reduce, reduce(expr))
    }

    (M.lookup(name, index, expr)) {
        M.lookup(name, index, reduce(expr))
    }

    (M.make_vec(size, args)) {
        M.make_vec(size, args |> reduce)
    }

    (M.match_cases(test, cases)) {
        M.match_cases(reduce(test), cases |> identity && reduce)
    }

    (M.namespaces(exprs)) {
        M.namespaces(exprs |> reduce)
    }

    (M.sequence(exprs)) {
        M.sequence(exprs |> reduce)
    }

    (x) {
        M.print_expr(x);
        puts("\n");
        error("reduce: unsupported expression")
    }
}