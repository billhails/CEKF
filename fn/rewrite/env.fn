namespace
    link "../dictutils.fn" as D;

    typedef Env(#k, #v) { root | env(D.Dict(#k, #v), Env(#k, #v)) }

    fn extend {
        (x=root) |
        (x=env(_, _)) {
            env(D.make([], []), x)
        }
    }

    fn add {
        (env(d, p), k, v) {
            env(D.insert(k, v, d), p)
        }
        (root, _, _) {
            error("Cannot add to root environment")
        }
    }

    fn add_lists {
        (env(d, p), k, v) {
            env(D.add_lists(k, v, d), p)
        }
        (root, _, _) {
            error("Cannot add to root environment")
        }
    }

    fn get {
        (root, _) {
            nothing
        }
        (env(dict, parent), k) {
            switch (D.lookup(k, dict)) {
                (nothing) { get(parent, k) }
                (x=some(_)) { x }
            }
        }
    }