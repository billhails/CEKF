namespace
    link "minexpr.fn" as M;
    link "../dictutils.fn" as D;
    link "../listutils.fn" as list;
    import list operators;

    // substitute: D.Dict(string, M.expr) -> M.expr -> M.expr
    fn substitute(c, e) {
        if (D.is_empty(c)) {
            e
        } else {
            switch (e) {
                // amb_expr(expr, expr)
                (M.amb_expr(expr1, expr2)) {
                    M.amb_expr(substitute(c, expr1), substitute(c, expr2))
                }

                // apply_closure(expr, list(expr))
                (M.apply_closure(f, args)) {
                    M.apply_closure(substitute(c, f), args |> substitute(c))
                }

                // apply(expr, list(expr))
                (M.apply(fun, args)) {
                    M.apply(substitute(c, fun), args |> substitute(c))
                }

                (x = M.back_expr) |
                (x = M.primop(_)) |
                (x = M.bigint(_)) |
                (x = M.character(_)) |
                (x = M.stdint(_)) {
                    x
                }

                // callcc_expr(expr)
                (M.callcc_expr(e)) {
                    M.callcc_expr(substitute(c, e))
                }

                // cond_expr(expr, list(#(expr, expr)))
                (M.cond_expr(test, branches)) {
                    M.cond_expr(substitute(c, test),
                                branches |> substitute(c) && substitute(c))
                }

                // if_expr(expr, expr, expr)
                (M.if_expr(exprc, exprt, exprf)) {
                    M.if_expr(substitute(c, exprc),
                            substitute(c, exprt),
                            substitute(c, exprf))
                }

                // lambda(list(string), expr)
                (M.lambda(params, body)) {
                    let c2 = D.delete_list(params, c);
                    in M.lambda(params, substitute(c2, body))
                }

                // lambda(list(string), expr)
                (M.lambdac(params, body)) {
                    let c2 = D.delete_list(params, c);
                    in M.lambdac(params, substitute(c2, body))
                }

                // make_closure(expr, env)
                (M.make_closure(body, env)) {
                    M.make_closure(substitute(c, body), substitute(c, env))
                }

                // make_env(list(#(string, expr)))
                (M.make_env(bindings)) {
                    M.make_env(bindings |> identity && substitute(c))
                }

                // env_ref(expr, string)
                (M.env_ref(e, s)) {
                    M.env_ref(substitute(c, e), s)
                }

                // letrec_expr(list(#(string, expr)), expr)
                (M.letrec_expr(bindings, expr)) {
                    let c2 = D.delete_list(bindings |> fn (#(var, val)) { var }, c);
                    in M.letrec_expr(bindings, substitute(c2, expr)) // new environment
                }

                // make_vec(number, list(expr))
                (M.make_vec(size, args)) {
                    M.make_vec(size, args |> substitute(c))
                }

                // match_cases(expr, list(#(list(number), expr)))
                (M.match_cases(test, cases)) {
                    let #(vals, results) = unzip cases;
                    in
                        M.match_cases(substitute(c, test),
                                      vals zip (results |> substitute(c)))
                }

                // sequence(list(expr))
                (M.sequence(exprs)) {
                    M.sequence(exprs |> substitute(c))
                }

                // var(string)
                (M.var(name)) {
                    switch (D.lookup(name, c)) {
                        (nothing) { e }
                        (just(v)) { v }
                    }
                }

                (x) {
                    M.print_expr(x);
                    puts("\n");
                    error("substitute: unsupported expression")
                }
            }
        }
    }