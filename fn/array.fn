namespace

link "dictionary.fn" as dic;

typedef Array(#t) { array(number, number, dic.Dict(number, #t)) }

print Array fn (pt, a = array(low, high, d) {
    let
        fn helper {
            (n)) {
                switch(dic.lookup(n, d)) {
                    (some(v)) {
                        pt(v);
                        if (n < high - 1) {
                            puts(", ");
                            helper(n + 1);
                        } else {
                            true;
                        }
                    }
                    (nothing) {
                        true;
                    }
                }
            }
        }
    in
        puts("[");
        helper(low);
        puts("]");
        a;
}

fn newArray() {
    array(0, 0, dic.E)
}

fn push (v, array(low, high, dict)) {
    array(low, high + 1, dic.insert(high, v, dict))
}

fn last (array(_, high, dict)) {
    dic.lookup(high - 1, dict)
}

fn first (array(low, _, dict)) {
    dic.lookup(low, dict)
}

fn pop {
    (x = array(low, low, _)) { x }
    (array(low, high, dict)) {
        array(low, high - 1, dic.delete(high - 1, dict))
    }
}

fn unshift(v, array(low, high, dict)) {
    array(low - 1, high, dic.insert(low - 1, v, dict))
}

fn shift {
    (x = array(low, low, _)) { x }
    (array(low, high, dict)) {
        array(low + 1, high, dic.delete(low, dict))
    }
}

fn inspect(n, array(low, _, dict)) {
    dic.lookup(n + low, dict)
}

fn size(array(low, high, _)) {
    high - low
}
