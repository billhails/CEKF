let
    // very simple environment
    typedef environment { frame(list(char), expression, environment) | root }

    // very simple AST
    typedef expression {
        addition(expression, expression) |
        subtraction(expression, expression) |
        multiplication(expression, expression) |
        division(expression, expression) |
        num(number) |
        symbol(list(char)) |
        conditional(expression, expression, expression) |
        lambda(expression, expression) |
        closure(expression, environment) |
        application(expression, expression)
    }

    // an interpreter
    fn eval {
        (addition(l, r), e)              { add(eval(l, e), eval(r, e)) }
        (subtraction(l, r), e)           { sub(eval(l, e), eval(r, e)) }
        (multiplication(l, r), e)        { mul(eval(l, e), eval(r, e)) }
        (division(l, r), e)              { div(eval(l, e), eval(r, e)) }
        (i = num(_), e)               { i }
        (symbol(s), e)                   { lookup(s, e) }
        (conditional(test, pro, con), e) { cond(test, pro, con, e) }
        (l = lambda(_, _), e)            { closure(l, e) }
        (application(function, arg), e)  { apply(eval(function, e), eval(arg, e)) }
    }

    // function application
    fn apply (closure(lambda(symbol(s), body), e), arg) {
        eval(body, frame(s, arg, e))
    }

    // built-ins
    fn add (num(a), num(b)) { num(a + b) }
    
    fn sub (num(a), num(b)) { num(a - b) }
    
    fn mul (num(a), num(b)) { num(a * b) }
    
    fn div (num(a), num(b)) { num(a / b) }
    
    fn cond(test, pro, con, e) {
        switch (eval(test, e)) {
            (num(0)) { eval(con, e) } // 0 is false
            (num(_)) { eval(pro, e) }
        }
    }

    // lookup access to the environment
    fn lookup {
        (s, frame(s, value, _))  { value }
        (s, frame(_, _, parent)) { lookup(s, parent) }
        (s, root)                { num(0) } // error("mce symbol not defined " @@ s) }
    }
in
    // try it out: ((lambda (x) (if x (+ x 2) x)) a) |- a: 3
    print(eval(
        application(
            lambda(
                symbol("x"),
                conditional(
                    symbol("x"),
                    addition(symbol("x"), num(2)),
                    symbol("x")
                )
            ),
            symbol("a")
        ),
        frame("a", num(3), root)
    ))
