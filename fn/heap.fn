let
    typedef heap(#t) { E | T(number, #t, heap(#t), heap(#t)) }

    typedef maybe(#t) { none | some(#t) }

    fn merge {
      (E, h) | (h, E) { h }
      (h1=T(_, x, a1, b1), h2=T(_, y, a2, b2)) {
        if (x <= y) {
          makeT(x, a1, merge(b1, h2))
        } else {
          makeT(y, a2, merge(h1, b2))
        }
      }
    }

    fn rank {
      (E) { 0 }
      (T(r, _, _, _)) { r }
    }

    fn makeT(x, a, b) {
      if (rank(a) >= rank(b)) {
        T(rank(b) + 1, x, a, b)
      } else {
        T(rank(a) + 1, x, b, a)
      }
    }

    fn insert(x, h) {
      merge(T(1, x, E, E), h)
    }

    fn findMin {
      (E)             { none }
      (T(_, x, _, _)) { some(x) }
    }

    fn deleteMin {
      (E)             { E }
      (T(_, _, a, b)) { merge(a, b) }
    }

    fn makeHeap(lst) {
       let fn helper {
           ([], heap) { heap }
           (h @ t, heap) { helper(t, insert(h, heap)) }
       }
       in
           helper(lst, E)
    }

    fn makeList(heap) {
        let
            fn helper {
                (hp, lst) {
                    switch(findMin(hp)) {
                        (some(min)) {
                            helper(deleteMin(hp), min @ lst)
                        }
                        (none) {
                            lst
                        }
                    }
                }
            }
        in
            helper(heap, [])
    }
 
in
    print(makeList(
        makeHeap(
            "kgimtseettepmupybbbmplgntqzutrfxqarkivirlbjqjigntslfewhnjouuyiepnswymkfpyovc"
            "lntwbmhngufnoeidjfmhxxaqmqiaoodslwlwwnzxtdxawnfxbiesjtdwmrzkbdozzyppmdyzhvyh"
            "fadldkflwiwvmfutfeckzsqulxlenvpwpbqdjwxpphhtyeuojmvmhgwcisxevzlvbtnobaaokqbu"
            "tzqumbzlgqwqsludnrygnynsqcvjekjouyyplgyzlhlsbakaknjauctsspolsvifpwrklfxjfbxr"
            "nkecgmypfkbxonkusuzigleakcrnqhktvjonlfiuoeoupuwdtzyytsmggyspdoswafjvceqyzgtk"
            "socdhszgybbrrivirlcgozvxgtvtxgyuukntggimfsoprufwmdngqkabxolitehdjbiqhdjexiao"
            "jatkhdjdcpckbxdxwfocqjchijeylcyhoxnywikniqzpqeeqtucengrvgbbntwmvyeoddjxlgqab"
            "lttpyrmxlckrvstwmvmfcvmbskcivvsjlchmoczdnbczvznnkiaabonyiqvjmbiyeddetcczprvj"
            "fmhmshxmknhonxyxgbgpawkahqbknbqpargbnnunwgxphhlvedlyazkfzavezdtlfefpvjiooocp"
            "wspyalpahadximvaauocirfgpijbhqskcuuynvcvtndifklhczerlvjpfpjhnkxxprrsktdfnrrm"
            "oypigecxbkzxflgtmdxxzfvdvesewzqotfdlqfibzkchndafnmxirrruol")))
