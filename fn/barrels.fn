let
    fn require(condition) {
        condition or back
    }

    fn member {
        (_, []) { false }
        (x,  x @ _) { true }
        (x, _ @ t) { member(x, t) }
    }

    fn one_of {
        ([]) { back }
        (h @ t) {
            h then one_of(t)
        }
    }

    fn exclude {
        (items, []) { [] }
        (items, h @ t) {
            if (member(h, items)) {
                exclude(items, t)
            } else {
                h @ exclude(items, t)
            }
        }
    }

    fn some_of {
        ([]) { back }
        (h @ t) { [h] then some_of(t) then h @ some_of(t) }
    }

    fn sum {
        ([]) { 0 }
        (h @ t) { h + sum(t) }
    }

    fn barrels_of_fun() {
            // A wine merchant has six barrels of wine and beer containing:
            //
            //    30 gallons
            //    32 gallons
            //    36 gallons
            //    38 gallons
            //    40 gallons
            //    62 gallons
            //
            // Five barrels are filled with wine and one with beer.
            // The first customer purchases two barrels of wine.
            // The second customer purchases twice as much wine as the first customer.
            // Which barrel contains beer? 
        let
            barrels = [30, 32, 36, 38, 40, 62];
            beer = one_of(barrels);
            wine = exclude([beer], barrels);
            barrel_1 = one_of(wine);
            barrel_2 = one_of(exclude([barrel_1], wine));
            purchase = some_of(exclude([barrel_1, barrel_2], wine));
        in
            require((barrel_1 + barrel_2) * 2 == sum(purchase));
            beer
    }
in
    barrels_of_fun()
